using terms from application "Mail"
	on perform mail action with messages theSelectedMessages for rule theRule
		repeat with theMessage in theSelectedMessages
			set messageContent to content of theMessage
			-- We really have to explicitly go through the entire email line by line to find the GPX Import URL: line?!
			repeat with aParagraph in (paragraphs of messageContent)
				set aParagraph to contents of aParagraph
				if aParagraph begins with "GPX" then
					set GPXURL to characters 17 thru end of aParagraph as string
					-- Download GPX to /tmp
					repeat 3 times
						set success to true
						try
							do shell script "cd /tmp; curl -fO " & quoted form of GPXURL & ""
						on error theError
							set success to false
							tell application "Mail" to log theError
						end try
						if success = true then exit repeat
						delay 15
					end repeat
					if success = false then
						display dialog "Unable to download GPX file from Abvio. Please do this manually"
						return -1
					end if
					set filename to do shell script "echo " & quoted form of GPXURL & " | awk 'BEGIN { FS=\"/\"}; {print $NF}'"
					exit repeat
				end if
			end repeat
		end repeat
		
		set theAttachment to POSIX path of "/tmp/" & filename
		set theMessage to make new outgoing message with properties {visible:true, subject:"Cyclemeter GPX Forwarder", content:""}
		-- Add sender:"email@address.com" to the end of this list if you have multiple accounts configured. Don't forget the comma after content:""
		tell content of theMessage
			make new attachment with properties {file name:theAttachment} at after last paragraph
		end tell
		tell theMessage
			make new to recipient at end of to recipients with properties {name:"Strava", address:"upload@strava.com"}
		end tell
		
		send theMessage
		
		do shell script "rm /tmp/" & filename
		
	end perform mail action with messages
	
end using terms from